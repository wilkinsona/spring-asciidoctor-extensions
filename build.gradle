plugins {
	id "com.jfrog.artifactory" version "4.4.13"
	id 'eclipse'
	id 'java'
	id 'maven'
}

group = 'io.spring.asciidoctor'
description = 'Spring Asciidoctor Extensions'

apply from: 'gradle/publish-maven.gradle'

repositories {
	mavenCentral()
}

ext {
	asciidoctorjVersion = '1.5.6'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
	jarjar
	asm
}

task repackageAsm(type: Jar) {
	baseName = 'repackaged-asm'

	doLast() {
		project.ant {
			taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask",
				classpath: configurations.jarjar.asPath
			jarjar(destfile: archivePath) {
				configurations.asm.each { originalJar ->
					zipfileset(src: originalJar, includes: '**/*.class')
				}
				rule(pattern: 'org.objectweb.**', result: 'io.spring.asciidoctor.@1')
			}
		}
	}
}

dependencies {
	asm 'org.ow2.asm:asm:6.0'
	compile files(repackageAsm)
	compileOnly "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	jarjar 'com.googlecode.jarjar:jarjar:1.3'
	testCompile "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	testCompile 'junit:junit:4.12'
	testCompile 'org.assertj:assertj-core:2.6.0'
	testCompile 'org.mockito:mockito-core:2.15.0'
}

jar {
	dependsOn repackageAsm
	from(zipTree(repackageAsm.archivePath)) {
		include "io/spring/asciidoctor/**"
	}
}

javadoc {
	title = "$project.description $project.version API"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

artifactory {
	contextUrl = 'https://repo.spring.io'
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			if (project.hasProperty('artifactoryUsername')
					&& project.hasProperty('artifactoryPassword')) {
				username = artifactoryUsername
				password = artifactoryPassword
			}
		}
	}
}

artifactoryPublish {
	publishConfigs 'archives'
	publishIvy false
}